import {
  Dict {dictionary},
  core.control {grow},
  core.int.ord,
  core.int.parse {parse-int},
  core.random {random},
}

constant intdict: Dict.trope(int) {
  Dict.from-ord(core.int.ord.as-ord-N)
}

define make-big-dict(size: int): dictionary(int, int) {
  grow(Dict.empty(), size, function (acc, _) {
    let key = random(1000000) in
    let val = random(1000000) in
    intdict::insert(key, val, acc)
  })
}

define random-lookup-sum(count: int, d: &dictionary(int, int)): unit {
  let rsum =
    grow(count, 0, function (acc, _) {
      let key = random(1000000) in
      let val on key =
        match intdict::lookup(key, d) {
        | Left(_) =>
          0
        | Right(v) =>
          *v
        }
      in
      let _ = key in
      add-int(acc, val)
    })
  in
  print-int(rsum)
}

define main(): unit {
  let args = get-argv() in
  match args {
  | [_, size-str] =>
    let _ on size-str =
      match parse-int(size-str) {
      | Right(size) =>
        let big-dict = make-big-dict(size) in
        let _ on big-dict = random-lookup-sum(size, big-dict) in
        let _ = big-dict in
        Unit
      | Left(_) =>
        printf("couldn't parse `{}` as an integer\n", [*size-str])
      }
    in
    let _ = size-str in
    Unit
  | _ =>
    print("usage: dict-nt SIZE\n")
  }
}
