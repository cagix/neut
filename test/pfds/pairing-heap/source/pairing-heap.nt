import {
- core.list
- core.option
- core.bool
}

variant heap(a) {
- Leaf
- Node(a, list(heap(a)))
}

variant order {
- LT
- EQ
- GT
}

define find-min[a](h: &heap(a)): ?a {
  case h {
  - Leaf =>
    None: ?a
  - Node(v, hs) =>
    Some(!v)
  }
}

define merge[a](cmp: (a, a) -> order, h1: heap(a), h2: heap(a)): heap(a) {
  match h1, h2 {
  - Leaf, h2 =>
    h2
  - h1, Leaf =>
    h1
  - Node(x1, hs1), Node(x2, hs2) =>
    match cmp(x1, x2) {
    - LT =>
      Node(x1, Node(x2, hs2) :< hs1)
    - otherwise =>
      Node(x2, Node(x1, hs1) :< hs2)
    }
  }
}

define insert[a](cmp: (a, a) -> order, x: a, h: heap(a)): heap(a) {
  merge(cmp, Node(x, []), h)
}

define merge-pairs[a](cmp: (a, a) -> order, hs: list(heap(a))): heap(a) {
  match hs {
  - [] =>
    Leaf
  - [h] =>
    h
  - h1 :< h2 :< rest =>
    merge(cmp, merge(cmp, h1, h2), merge-pairs(cmp, rest))
  }
}

define delete-min[a](cmp: (a, a) -> order, h: heap(a)): ?heap(a) {
  match h {
  - Leaf =>
    None
  - Node(x, hs) =>
    Some(merge-pairs(cmp, hs))
  }
}

define compare-int(x: i64, y: i64): order {
  if lt-i64(x, y) {
    LT
  } else-if gt-i64(x, y) {
    GT
  } else {
    EQ
  }
}

define main(): i64 {
  let h: heap(i64) = Leaf
  let h = insert(compare-int, 10, h)
  let h = insert(compare-int, 3, h)
  let h = insert(compare-int, 8, h)
  let h = insert(compare-int, 2, h)
  let maybe-min on h = {
    match find-min(h) {
    - None =>
      10
    - Some(min) =>
      min
    }
  }
  maybe-min
}
