import {
- core.list
- core.bool
- core.option
}

use {
- core.list
- core.bool
- core.option
}

struct product(a, b) {
- left: a
- right: b
}

define-inline queue(a: tau): tau {
  list(a) * list(a)
}

define empty<a>(): queue(a) {
  tuple([], [])
}

define reverse<a>(xs: list(a)): list(a) {
  let f =
    define helper(ys: list(a), acc: list(a)): list(a) {
      match ys {
      - [] =>
        acc
      - z :: zs =>
        helper(zs, z :: acc)
      }
    }
  f(xs, [])
}

define head<a>(q: &queue(a)): ?a {
  case q {
  - tuple(x :: rest, ys) =>
    option.Some(!x)
  - otherwise =>
    option.None()
  }
}

define-inline sanitize<a>(q: queue(a)): queue(a) {
  match q {
  - tuple([], ys) =>
    tuple(reverse(ys), [])
  - q =>
    q
  }
}

define tail<a>(q: queue(a)): ?queue(a) {
  match q {
  - tuple(x :: rest, ys) =>
    option.Some(sanitize(tuple(rest, ys)))
  - otherwise =>
    option.None()
  }
}

define snoc<a>(v: a, q: queue(a)): queue(a) {
  let tuple(xs, ys) = q
  sanitize(tuple(xs, v :: ys))
}

define sum(xs: &list(i64)): i64 {
  case xs {
  - [] =>
    0
  - v :: rest =>
    add-i64(!v, sum(rest))
  }
}

define main(): i64 {
  let q: queue(list(i64)) = empty()
  let q = snoc([2], q)
  let q = snoc([5], q)
  let q = snoc([1], q)
  let v1 on q = {
    match head(q) {
    - option.Some(xs1) =>
      let v1 on xs1 = sum(xs1)
      v1
    - option.None() =>
      4
    }
  }
  let mq = tail(q)
  match mq {
  - option.None() =>
    1
  - option.Some(q) =>
    let q = snoc([3], q)
    let q = snoc([8], q)
    let v2 on q = {
      match head(q) {
      - option.Some(xs) =>
        let v on xs = sum(xs)
        v
      - option.None() =>
        4
      }
    }
    add-i64(v1, v2)
  }
}
