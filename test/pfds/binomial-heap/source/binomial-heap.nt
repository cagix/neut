data tree(a) {
  // the list is in *decreasing* order of rank
- Node(int, a, list(tree(a)))
}

inline heap(a: tau): tau {
  // the list is in *increasing* order of rank
  list(tree(a))
}

data order {
- LT
- EQ
- GT
}

define link[a](cmp: (a, a) -> order, t1: tree(a), t2: tree(a)): tree(a) {
  match t1, t2 {
  - Node(r1, x1, children1), Node(r2, x2, children2) =>
    match cmp(x1, x2) {
    - LT =>
      Node(add-int(r1, 1), x1, Node(r2, x2, children2) :: children1)
    - _ =>
      Node(add-int(r1, 1), x2, Node(r1, x1, children1) :: children2)
    }
  }
}

define rank[a](t: &tree(a)): int {
  case t {
  - Node(r, _, _) =>
    *r
  }
}

define insert-tree[a](cmp: (a, a) -> order, t: tree(a), h: heap(a)): heap(a) {
  match h {
  - [] =>
    [t]
  - t1 :: rest =>
    let r on t = rank(t)
    let r1 on t1 = rank(t1)
    if lt-int(r, r1) {
      t :: t1 :: rest
    } else {
      // the link here corresponds to a carry in binary arithmetic
      insert-tree(cmp, link(cmp, t, t1), rest)
    }
  }
}

define insert[a](cmp: (a, a) -> order, value: a, h: heap(a)): heap(a) {
  insert-tree(cmp, Node(0, value, []), h)
}

define merge[a](cmp: (a, a) -> order, h1: heap(a), h2: heap(a)): heap(a) {
  match h1, h2 {
  - [], h2 =>
    h2
  - h1, [] =>
    h1
  - t1 :: ts1, t2 :: ts2 =>
    let r1 on t1 = rank(t1)
    let r2 on t2 = rank(t2)
    if lt-int(r1, r2) {
      t1 :: merge(cmp, ts1, t2 :: ts2)
    } else-if gt-int(r1, r2) {
      t2 :: merge(cmp, t1 :: ts1, ts2)
    } else {
      insert-tree(cmp, link(cmp, t1, t2), merge(cmp, ts1, ts2))
    }
  }
}


define root[a](t: &tree(a)): a {
  case t {
  - Node(_, v, _) =>
    *v
  }
}

define find-min[a](cmp: (a, a) -> order, h: &heap(a)): ?a {
  let helper =
    mu self(cand: a, h: &heap(a)): a {
      case h {
      - [] =>
        cand
      - t :: ts =>
        let root-head = root(t)
        match cmp(root-head, cand) {
        - LT =>
          self(root-head, ts)
        - _ =>
          self(cand, ts)
        }
      }
    }
  case h {
  - [] =>
    None
  - t :: ts =>
    let r = root(t)
    Some(helper(r, ts))
  }
}

define remove-min-tree[a](cmp: (a, a) -> order, h: heap(a)): ?tuple(tree(a), heap(a)) {
  match h {
  - [] =>
    None
  - [t] =>
    Some(Tuple(t, []))
  - t :: ts =>
    let? Tuple(cand, ts-adjusted) = remove-min-tree(cmp, ts)
    let c on t, cand = {
      let root-head = root(t)
      let root-cand = root(cand)
      cmp(root-head, root-cand)
    }
    match c {
    - LT =>
      Some(Tuple(t, cand :: ts-adjusted))
    - _ =>
      Some(Tuple(cand, t :: ts-adjusted))
    }
  }
}

define delete-min[a](cmp: (a, a) -> order, h: heap(a)): ?heap(a) {
  let? Tuple(Node(_, _, ts1), ts2): tuple(tree(a), heap(a)) = remove-min-tree(cmp, h)
  Some(merge(cmp, reverse(ts1), ts2))
}

define compare-int(x: int, y: int): order {
  if lt-int(x, y) {
    LT
  } else-if gt-int(x, y) {
    GT
  } else {
    EQ
  }
}

define main(): unit {
  let h: heap(int) = []
  let h = insert(compare-int, 3, h)
  let h = insert(compare-int, 4, h)
  let h = insert(compare-int, 2, h)
  let h = insert(compare-int, 9, h)
  let h = insert(compare-int, 100, h)
  let h = insert(compare-int, 8, h)
  let h = insert(compare-int, 1, h)
  let h = merge(compare-int, h, h)
  let maybe-min on h =
    match find-min(compare-int, h) {
    - None =>
      10
    - Some(min) =>
      min
    }
  let _ = h
  print-int(maybe-min)
  print("\n")
}
