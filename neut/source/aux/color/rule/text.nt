import {
  ansi.color {Blue, Red},
  ansi.style {Bold, Italic, Normal},
  text-builder.entity,
  text-builder.scene {append-text},
  this.aux.color.rule.tone {Background, Foreground, Style, _append-tone, _append-tones, tone},
}

data color-text {
| Empty
| Join(list(tone), text, color-text)
}

define pack(ts: list(tone), t: text): color-text {
  Join(ts, t, Empty)
}

define pack'(t: text): color-text {
  Join([], t, Empty)
}

define _unpack-with-tone(t: &color-text): text {
  let b = text-builder.entity.create(64) in
  pin reset = Style(Normal) in
  let _ on b =
    let f =
      define loop(t: &color-text): unit {
        case t {
        | Empty =>
          Unit
        | Join(ts, t, rest) =>
          _append-tones(b, ts);
          append-text(b, t);
          _append-tone(b, reset);
          loop(rest)
        }
      }
    in
    f(t)
  in
  text-builder.entity.get(b)
}

define _unpack-without-tone(t: &color-text): text {
  let b = text-builder.entity.create(256) in
  let _ on b =
    let f =
      define loop(t: &color-text): unit {
        case t {
        | Empty =>
          Unit
        | Join(_, t, rest) =>
          append-text(b, t);
          loop(rest)
        }
      }
    in
    f(t)
  in
  text-builder.entity.get(b)
}

define zen(): unit {
  pin t =
    Join(
      [Foreground(Blue), Style(Bold)],
      *"hello",
      Join([], *"\n", Empty),
    )
  in
  pin fragment1 = _unpack-with-tone(t) in
  pin fragment2 = _unpack-without-tone(t) in
  pin t' = pack([Background(Red), Style(Italic)], *"sample\n") in
  pin fragment3 = _unpack-with-tone(t') in
  print(fragment1);
  print(fragment2);
  print(fragment3);
  print("sample\n")
}
