import {
  ansi.color {Blue, Magenta, Red},
  ansi.style {Bold},
  this.color.rule.tone {Foreground, Style, tone},
}

data log-level {
| Note
| Warning
| Error
| Critical
}

define _log-level-to-text(l: log-level): &text {
  match l {
  | Note =>
    "Note"
  | Warning =>
    "Warning"
  | Error =>
    "Error"
  | Critical =>
    "Critical"
  }
}

define _log-level-to-tone(l: log-level): list(tone) {
  match l {
  | Note =>
    [Style(Bold), Foreground(Blue)]
  | Warning =>
    [Style(Bold), Foreground(Magenta)]
  | Error =>
    [Style(Bold), Foreground(Red)]
  | Critical =>
    [Style(Bold), Foreground(Red)]
  }
}

define _log-level-to-pad(l: log-level): text {
  let len = core.text.length(_log-level-to-text(l)) in
  core.text.replicate(" ", add-int(len, 2))
}
